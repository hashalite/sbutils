plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.mc_version
group = project.maven_group

repositories {
    maven { url "https://maven.isxander.dev/releases" }
    maven { url "https://maven.terraformersmc.com/" }
    maven {url "https://oss.sonatype.org/content/repositories/snapshots/"}
    maven { url "https://maven.parchmentmc.org/" }
}

loom {
    accessWidenerPath = file("src/main/resources/sbutils.accesswidener")
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.mc_version}"

    def parchmentAppendix = rootProject.parchment_version.split('-')[0]
    def parchmentVersion = rootProject.parchment_version.split('-')[1]
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchmentAppendix}:${parchmentVersion}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation ("com.terraformersmc:modmenu:${project.modmenu_version}") {
        exclude module: "fabric-api"
    }

    modApi "dev.isxander.yacl:yet-another-config-lib-fabric:${project.yacl_version}"
    include "dev.isxander.yacl:yet-another-config-lib-fabric:${project.yacl_version}"

    modApi group: 'net.objecthunter', name: 'exp4j', version: project.exp4j_version
    include group: 'net.objecthunter', name: 'exp4j', version: project.exp4j_version

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
